PROVIDE(_stext = ORIGIN(REGION_TEXT));

SECTIONS {
    .text _stext :
    {
        PROVIDE(__kernel_start = .);
        __stext = .;

        *(.text.entry);
        *(.text .text.*);

    } > REGION_TEXT AT> LINK_REGION_TEXT

    __etext = .;

    .rodata : ALIGN(16)
    {
        __srodata = .;

        *(.rodata .rodata.*);
        
        . = ALIGN(8);

        PROVIDE(__eh_frame = .);
        PROVIDE(__executable_start = __stext);

        KEEP(*(.eh_frame_hdr));
        KEEP(*(.eh_frame));
        KEEP(*(.eh_frame.*));

    } > REGION_RODATA AT> LINK_REGION_RODATA

    __erodata = .;

    .data : ALIGN(16)
    {
        __sdata = .;

        *(.data .data.*);
        *(.got .got.plt);

    } > REGION_DATA AT> LINK_REGION_DATA

    .data.after :
    {
        __data_after = .;
    } > REGION_DATA AT> LINK_REGION_DATA

    __edata = .;

    .bss (NOLOAD) : ALIGN(16)
    {
        __sbss = .;

        *(.bss .bss.*);

        . = ALIGN(0x1000);
    } > REGION_BSS AT> LINK_REGION_BSS

    __ebss = .;

    . = ALIGN(0x1000);
}

SECTIONS {
    /* Stabs debugging sections.  */
    .stab          0 : { KEEP(*(.stab)); }
    .stabstr       0 : { KEEP(*(.stabstr)); }
    .stab.excl     0 : { KEEP(*(.stab.excl)); }
    .stab.exclstr  0 : { KEEP(*(.stab.exclstr)); }
    .stab.index    0 : { KEEP(*(.stab.index)); }
    .stab.indexstr 0 : { KEEP(*(.stab.indexstr)); }
    .comment       0 : { KEEP(*(.comment)); }
    /* DWARF debug sections.
       Symbols in the DWARF debugging sections are relative to the beginning
       of the section so we begin them at 0.  */
    /* DWARF 1 */
    .debug          0 : { KEEP(*(.debug)); }
    .line           0 : { KEEP(*(.line)); }
    /* GNU DWARF 1 extensions */
    .debug_srcinfo  0 : { KEEP(*(.debug_srcinfo)); }
    .debug_sfnames  0 : { KEEP(*(.debug_sfnames)); }
    /* DWARF 1.1 and DWARF 2 */
    .debug_aranges  0 : { KEEP(*(.debug_aranges)); }
    .debug_pubnames 0 : { KEEP(*(.debug_pubnames)); }
    /* DWARF 2 */
    .debug_info     0 : { KEEP(*(.debug_info)); }
    .debug_abbrev   0 : { KEEP(*(.debug_abbrev)); }
    .debug_line     0 : { KEEP(*(.debug_line)); }
    .debug_frame    0 : { KEEP(*(.debug_frame)); }
    .debug_str      0 : { KEEP(*(.debug_str)); }
    .debug_loc      0 : { KEEP(*(.debug_loc)); }
    .debug_macinfo  0 : { KEEP(*(.debug_macinfo)); }
    /* SGI/MIPS DWARF 2 extensions */
    .debug_weaknames 0 : { KEEP(*(.debug_weaknames)); }
    .debug_funcnames 0 : { KEEP(*(.debug_funcnames)); }
    .debug_typenames 0 : { KEEP(*(.debug_typenames)); }
    .debug_varnames  0 : { KEEP(*(.debug_varnames)); }

    /* DWARF Other */
    .debug_ranges  0 : { KEEP(*(.debug_ranges)); }
    .debug_line_str 0 : { KEEP(*(.debug_line_str)); }

    /DISCARD/ :
    {
        *(.fini_array*)
        *(.note*)
        *(.dtors*)
        *(.debug_gdb_scripts*)
    }
}
